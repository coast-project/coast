# vim: set et ai ts=4 sw=4:
# -------------------------------------------------------------------------
# Copyright (c) 2010, Peter Sommerlad and IFS Institute for Software
# at HSR Rapperswil, Switzerland
# All rights reserved.
#
# This library/application is free software; you can redistribute and/or
# modify it under the terms of the license that is included with this
# library/application in the file license.txt.
# -------------------------------------------------------------------------

import pkg_resources
pkg_resources.require(["SConsider"])
import os
import fnmatch
import shutil
from stat import *
from SomeUtils import listFiles

Import('*')

searchReplace = {
    "LOGDIR": lambda env: env.getTargetBaseInstallDir().Dir('tmp').abspath,
    "ROTATEDIR": lambda env: env.getTargetBaseInstallDir().Dir('tmp').abspath,
}


def setUp(target, source, env):
    env['ENV']['COAST_USE_MMAP_STREAMS'] = 0
    #    env['ENV']['COAST_LOGONCERR']=3
    #    env['ENV']['COAST_TRACE_STORAGE']=0
    #    env['ENV']['COAST_TRACE_INITFINIS']=1
    targetDir = env.getTargetBaseInstallDir()
    tmppath = targetDir.Dir('tmp').abspath
    for d in [tmppath, targetDir.Dir(os.path.join('config', 'rotate')).get_abspath()]:
        if os.path.isdir(d):
            shutil.rmtree(d)
        try:
            os.makedirs(d)
        except oserror:
            pass


def tearDown(target, source, env):
    dir = env.getTargetBaseInstallDir().Dir('config')
    for root, dirs, files in os.walk(dir.abspath):
        for f in files:
            if fnmatch.fnmatch(f, '*.log*'):
                os.unlink(os.path.join(root, f))


programDepends = [
    'testfwWDBase.testfwWDBase',
    'CoastAppLog.CoastAppLog',
]
sourceFiles = listFiles(['*.cpp'])
copyFiles = [
    (listFiles(['config/*.any']), S_IRUSR | S_IRGRP | S_IROTH, searchReplace),
]
runConfig = {
    'setUp': setUp,
    'tearDown': tearDown,
}

_sconsider_dist = pkg_resources.get_distribution("SConsider").parsed_version
if _sconsider_dist < pkg_resources.parse_version("0.5"):
    from SConsider import createTargets
    buildSettings = {
        packagename: {
            'linkDependencies': programDepends,
            'sourceFiles': sourceFiles,
            'targetType': 'ProgramTest',
            'copyFiles': copyFiles,
            'runConfig': runConfig,
        },
    }

    createTargets(packagename, buildSettings)
elif _sconsider_dist >= pkg_resources.parse_version("0.5"):
    from TargetMaker import EnvBuilder
    from TargetHelpers import createCopyFilesTarget, createPackageTestTarget, createPackageProgram, createPackageProgramScript, createAlias
    reltargetdir = os.path.join('tests', packagename)

    def setRelTargetDir(env):
        env.setRelativeTargetDirectory(reltargetdir)

    bin_name = packagename
    bin_target = createPackageProgram(packagename,
                                      bin_name,
                                      sourceFiles,
                                      depend=programDepends,
                                      pkg_local=[setRelTargetDir])
    bins = EnvBuilder().depend(bin_target).build().RegisterPackageTarget(packagename=packagename,
                                                                         targetname='bins')

    copyTarget = createCopyFilesTarget(packagename,
                                       'configfiles',
                                       lambda env: env.getTargetBaseInstallDir(),
                                       copyFiles,
                                       pkg_local=[setRelTargetDir])

    scriptTarget = createPackageProgramScript(packagename,
                                              bin_name,
                                              bin_target,
                                              depend=copyTarget,
                                              pkg_local=[setRelTargetDir])
    scripts = EnvBuilder().depend(scriptTarget).build().RegisterPackageTarget(packagename=packagename,
                                                                              targetname='scripts')

    testTarget = createPackageTestTarget(packagename, bin_name, scriptTarget, settings=runConfig)

    createAlias('tests', testTarget)

    EnvBuilder().depend(bins, scripts).build().RegisterPackageTarget(packagename=packagename,
                                                                     targetname=packagename)

# vim: set et ai ts=4 sw=4:
# -------------------------------------------------------------------------
# Copyright (c) 2010, Peter Sommerlad and IFS Institute for Software
# at HSR Rapperswil, Switzerland
# All rights reserved.
#
# This library/application is free software; you can redistribute and/or
# modify it under the terms of the license that is included with this
# library/application in the file license.txt.
# -------------------------------------------------------------------------

import pkg_resources
pkg_resources.require(["SConsider"])
from SomeUtils import listFiles
Import('*')

sourceFiles = listFiles(['*.cpp'])

_sconsider_dist = pkg_resources.get_distribution('SConsider').parsed_version
if _sconsider_dist < pkg_resources.parse_version("0.5"):
    from SConsider import createTargets
    buildSettings = {
        packagename: {
            'linkDependencies': [
                packagename + '.lib_testfw',
            ],
            'targetType': 'IncludeOnly',
            'public': {
                'includes': listFiles(['*.h']),
            },
        },
        'lib_testfw': {
            'sourceFiles': sourceFiles,
            'targetName': 'testfw',
            'targetType': 'LibraryShared',
            'lazylinking': True,
        },
    }

    createTargets(packagename, buildSettings)
elif _sconsider_dist >= pkg_resources.parse_version("0.5"):
    from TargetMaker import EnvBuilder
    from TargetHelpers import createPackageSharedLibrary,\
        createPackageStaticLibrary
    includeTarget = EnvBuilder(export=[EnvBuilder().include(Dir('.'))]).build().RegisterPackageTarget(
        packagename=packagename, targetname='include')

    libDepends = []

    libbuilderfunc = createPackageSharedLibrary
    if EnvBuilder().build()["PLATFORM"] == "win32":
        libbuilderfunc = createPackageStaticLibrary

    lib = libbuilderfunc(packagename,
                         packagename,
                         listFiles(['*.cpp']),
                         depend=[includeTarget] + libDepends,
                         pkg_local=[lambda env: env.allowUnresolvedLinkSymbols()])

    libs = EnvBuilder().depend(lib).build().RegisterPackageTarget(packagename=packagename, targetname='libs')

    EnvBuilder().depend(includeTarget, libs).build().RegisterPackageTarget(packagename=packagename,
                                                                           targetname=packagename)
